CCEffect %{
techniques:
  - passes:
      - vert: sprite-vs:vert
        frag: sprite-fs:frag
        depthStencilState:
          depthTest: false
          depthWrite: false
        blendState:
          targets:
            - blend: true
              blendSrc: src_alpha
              blendDst: one_minus_src_alpha
              blendDstAlpha: one_minus_src_alpha
        rasterizerState:
          cullMode: none

        properties:
          dissolve: { value: grey, editor: { tooltip: "噪声贴图" } }
          dissolveThreshold:
            {
              value: 0.5,
              editor: { range: [0, 1, 0.01], slide: true, tooltip: "溶解阈值" },
            }
          dissolveColor:
            {
              value: [0.6, 0.2, 0.1, 1],
              editor: { tooltip: "溶解颜色", type: color },
            }

}%

CCProgram sprite-vs %{
  #include "../chunks/normal-vert.chunk"
}%

CCProgram sprite-fs %{
  precision highp float;
  #include <builtin/internal/sprite-texture>
  in vec2 uv0;

  #if USE_LOCAL
    in vec4 v_color;
  #endif

  uniform sampler2D dissolve;

  uniform Dissolve {
    vec4 dissolveColor;
    float dissolveThreshold;
  };

  vec4 frag() {
    vec4 dissolveMap = texture(dissolve, uv0);
    float value = dissolveMap.r;
    
    if (value < dissolveThreshold) {
      discard;
    }
    
    vec4 color = texture(cc_spriteTexture, uv0);
    #if USE_LOCAL
      color *= v_color;
    #endif
    
    if (value < dissolveThreshold + 0.05) {
      color = vec4(dissolveColor.rgb, color.a);
    }
    return color;
  }
}%
